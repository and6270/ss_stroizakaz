Функция СведенияОВнешнейОбработке() Экспорт
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Вид", "ДополнительныйОтчет");
	
	ПараметрыРегистрации.Вставить("Наименование", "Приход расход для снабжения");
	ПараметрыРегистрации.Вставить("Версия", "2.0");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	ПараметрыРегистрации.Вставить("Информация", "Приход расход для снабжения");
	ПараметрыРегистрации.Вставить("ВерсияБСП", "2.0.7.6");
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд,
	"Обороты товаров без перемещения",
	"Обороты",
	"ОткрытиеФормы",
	Истина,""
	
	);
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	Возврат ПараметрыРегистрации;
КонецФункции

Функция ПолучитьТаблицуКоманд()
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	Возврат Команды;
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	//Программно формируем отчет
	//Отменяем стандартную обработку
	СтандартнаяОбработка = Ложь;
	//Получаем схему
	Схема = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	//...и настройки
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	//Создаем компоновщик макета и получаем макет компоновки
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, ДанныеРасшифровки);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ОстаткиНоменклатураМесяцДо
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(ДОБАВИТЬКДАТЕ(&КонецПериода, ДЕНЬ, -30), Склад В (&Склады)) КАК ТоварыНаСкладахОстатки
	|ГДЕ
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК КоличествоВсего,
	|	ВЫРАЗИТЬ(ТоварыНаСкладахОстатки.Номенклатура КАК Справочник.Номенклатура).Код КАК Код,
	|	ВЫРАЗИТЬ(ТоварыНаСкладахОстатки.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры,
	|	ВЫРАЗИТЬ(ТоварыНаСкладахОстатки.Номенклатура КАК Справочник.Номенклатура).ЦеноваяГруппа КАК ЦеноваяГруппа
	|ПОМЕСТИТЬ ОстаткиНоменклатура
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&КонецПериода,
	|			Склад В (&Склады)
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						ОстаткиНоменклатураМесяцДо.Номенклатура
	|					ИЗ
	|						ОстаткиНоменклатураМесяцДо)) КАК ТоварыНаСкладахОстатки
	|ГДЕ
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстаткиИОбороты.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстаткиИОбороты.Период КАК Период,
	|	ТоварыНаСкладахОстаткиИОбороты.ВНаличииНачальныйОстаток КАК ВНаличииНачальныйОстаток,
	|	ТоварыНаСкладахОстаткиИОбороты.ВНаличииКонечныйОстаток КАК ВНаличииКонечныйОстаток
	|ПОМЕСТИТЬ Движения
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Секунда,
	|			,
	|			Склад В (&Склады)
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						ОстаткиНоменклатура.Номенклатура
	|					ИЗ
	|						ОстаткиНоменклатура)) КАК ТоварыНаСкладахОстаткиИОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Движения.Номенклатура КАК Номенклатура,
	|	Движения.Период КАК Период,
	|	Движения.ВНаличииНачальныйОстаток КАК ВНаличииНачальныйОстаток,
	|	Движения.ВНаличииКонечныйОстаток КАК ВНаличииКонечныйОстаток
	|ПОМЕСТИТЬ ДополнительныеДвижения
	|ИЗ
	|	Движения КАК Движения
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачальныеНули.Номенклатура,
	|	&НачалоПериода,
	|	0,
	|	0
	|ИЗ
	|	Движения КАК НачальныеНули
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КонечныеНули.Номенклатура,
	|	&КонецПериода,
	|	0,
	|	0
	|ИЗ
	|	Движения КАК КонечныеНули
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Точки.Номенклатура КАК Номенклатура,
	|	Точки.Период КАК Период,
	|	СУММА(Точки.ВНаличииНачальныйОстаток) КАК ВНаличииНачальныйОстаток,
	|	МАКСИМУМ(Точки.ВНаличииКонечныйОстаток) КАК ВНаличииКонечныйОстаток
	|ПОМЕСТИТЬ ТочкиВремени
	|ИЗ
	|	ДополнительныеДвижения КАК Точки
	|
	|СГРУППИРОВАТЬ ПО
	|	Точки.Номенклатура,
	|	Точки.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Точки.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(Слева.Период) КАК НачалоОтсутствия,
	|	Точки.Период КАК Период
	|ПОМЕСТИТЬ Интервалы
	|ИЗ
	|	ТочкиВремени КАК Точки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТочкиВремени КАК Слева
	|		ПО (Слева.Номенклатура = Точки.Номенклатура)
	|			И (Точки.ВНаличииНачальныйОстаток = 0)
	|			И (Слева.ВНаличииКонечныйОстаток = 0)
	|			И Точки.Период > Слева.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	Точки.Номенклатура,
	|	Точки.Период
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Точки.Номенклатура,
	|	&КонецПериода,
	|	&НачалоПериода
	|ИЗ
	|	Движения КАК Точки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Интервалы.Номенклатура КАК НоменклатураДнейНаличия,
	|	ВЫРАЗИТЬ(СУММА(РАЗНОСТЬДАТ(Интервалы.Период, Интервалы.НачалоОтсутствия, СЕКУНДА)) / 60 / 60 / 24 КАК ЧИСЛО(15, 0)) КАК ДнейНаличия
	|ПОМЕСТИТЬ ВремДнейНаличия
	|ИЗ
	|	Интервалы КАК Интервалы
	|
	|СГРУППИРОВАТЬ ПО
	|	Интервалы.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Движения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДополнительныеДвижения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТочкиВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Интервалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура КАК ВыручкаНоменклатура,
	|	ВыручкаИСебестоимостьПродаж.Количество КАК КоличествоПродаж,
	|	ВыручкаИСебестоимостьПродаж.СуммаВыручки КАК СуммаПродаж,
	|	ВыручкаИСебестоимостьПродаж.Период КАК Период,
	|	ВыручкаИСебестоимостьПродаж.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВремПродажи
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|ГДЕ
	|	ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ ВыручкаИСебестоимостьПродаж.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|	И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
	|	И ВыручкаИСебестоимостьПродаж.Склад В(&Склады)
	|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура В
	|			(ВЫБРАТЬ
	|				ОстаткиНоменклатура.Номенклатура
	|			ИЗ
	|				ОстаткиНоменклатура)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремПродажи.ВыручкаНоменклатура КАК ВыручкаНоменклатура,
	|	МАКСИМУМ(ВремПродажи.Период) КАК ДатаПоследнейПродажи,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВремПродажи.Регистратор) КАК КоличествоДокументов,
	|	СУММА(ВремПродажи.СуммаПродаж) КАК СуммаПродаж,
	|	СУММА(ВремПродажи.КоличествоПродаж) КАК КоличествоПродаж
	|ПОМЕСТИТЬ Продажи
	|ИЗ
	|	ВремПродажи КАК ВремПродажи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремПродажи.ВыручкаНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВремПродажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладах.Номенклатура КАК НоменклатураЗакупки,
	|	СУММА(ТоварыНаСкладах.ВНаличии) КАК КоличествоЗакупок
	|ПОМЕСТИТЬ ВремКоличествоЗакупок
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|ГДЕ
	|	ТоварыНаСкладах.Номенклатура В
	|			(ВЫБРАТЬ
	|				ОстаткиНоменклатура.Номенклатура
	|			ИЗ
	|				ОстаткиНоменклатура)
	|	И ТоварыНаСкладах.Склад В(&Склады)
	|	И ТИПЗНАЧЕНИЯ(ТоварыНаСкладах.Регистратор) = ТИП(Документ.ПриобретениеТоваровУслуг)
	|	И ТоварыНаСкладах.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыНаСкладах.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Закупки.Период КАК Период,
	|	Закупки.Количество КАК Количество,
	|	Закупки.Сумма КАК Сумма,
	|	Закупки.АналитикаУчетаНоменклатуры.Номенклатура КАК АналитикаУчетаНоменклатурыНоменклатура
	|ПОМЕСТИТЬ ВремВсеЗакупки
	|ИЗ
	|	РегистрНакопления.Закупки КАК Закупки
	|ГДЕ
	|	Закупки.Склад В(&Склады)
	|	И Закупки.АналитикаУчетаНоменклатуры.Номенклатура В
	|			(ВЫБРАТЬ
	|				ОстаткиНоменклатура.Номенклатура
	|			ИЗ
	|				ОстаткиНоменклатура)
	|	И ТИПЗНАЧЕНИЯ(Закупки.Регистратор) = ТИП(Документ.ПриобретениеТоваровУслуг)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВремВсеЗакупки.Период) КАК ДатаПоследнейЗакупки,
	|	ВремВсеЗакупки.АналитикаУчетаНоменклатурыНоменклатура КАК НоменклатураСС
	|ПОМЕСТИТЬ ВремДатаПоследнейЗакупки
	|ИЗ
	|	ВремВсеЗакупки КАК ВремВсеЗакупки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремВсеЗакупки.АналитикаУчетаНоменклатурыНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремДатаПоследнейЗакупки.НоменклатураСС КАК НоменклатураСС,
	|	ВЫРАЗИТЬ(ВремВсеЗакупки.Сумма / ВремВсеЗакупки.Количество КАК ЧИСЛО(15, 2)) КАК Себестоимость
	|ПОМЕСТИТЬ ВремСебестоимость
	|ИЗ
	|	ВремДатаПоследнейЗакупки КАК ВремДатаПоследнейЗакупки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремВсеЗакупки КАК ВремВсеЗакупки
	|		ПО ВремДатаПоследнейЗакупки.ДатаПоследнейЗакупки = ВремВсеЗакупки.Период
	|			И ВремДатаПоследнейЗакупки.НоменклатураСС = ВремВсеЗакупки.АналитикаУчетаНоменклатурыНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВремВсеЗакупки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВремДатаПоследнейЗакупки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВремЗакупочнаяЦена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&КонецПериода,
	|			ВидЦены = &ЗакупочнаяЦена
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						ОстаткиНоменклатура.Номенклатура
	|					ИЗ
	|						ОстаткиНоменклатура)) КАК ЦеныНоменклатурыСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.Период КАК ДатаРозница
	|ПОМЕСТИТЬ ВремРозничнаяЦена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|			&КонецПериода,
	|			ВидЦены = &РозничнаяЦена
	|				И Номенклатура В
	|					(ВЫБРАТЬ
	|						ОстаткиНоменклатура.Номенклатура
	|					ИЗ
	|						ОстаткиНоменклатура)) КАК ЦеныНоменклатурыСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладах.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ТоварыНаСкладах.Период) КАК Период,
	|	ТоварыНаСкладах.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВремВсеПоследнееПоступление
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|ГДЕ
	|	ТоварыНаСкладах.Номенклатура В
	|			(ВЫБРАТЬ
	|				ОстаткиНоменклатура.Номенклатура
	|			ИЗ
	|				ОстаткиНоменклатура)
	|	И ТоварыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ТИПЗНАЧЕНИЯ(ТоварыНаСкладах.Регистратор) = ТИП(Документ.ПриобретениеТоваровУслуг)
	|	И ВЫРАЗИТЬ(ТоварыНаСкладах.Регистратор КАК Документ.ПриобретениеТоваровУслуг).ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыНаСкладах.Номенклатура,
	|	ТоварыНаСкладах.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВремВсеПоследнееПоступление.Период) КАК Период,
	|	ВремВсеПоследнееПоступление.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВремДатыПоследнееПоступление
	|ИЗ
	|	ВремВсеПоследнееПоступление КАК ВремВсеПоследнееПоступление
	|
	|СГРУППИРОВАТЬ ПО
	|	ВремВсеПоследнееПоступление.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВремВсеПоследнееПоступление.Регистратор КАК Регистратор,
	|	ВремВсеПоследнееПоступление.Номенклатура КАК Номенклатура,
	|	ВремВсеПоследнееПоступление.Период КАК Период
	|ПОМЕСТИТЬ ВремРегистраторПоследнееПоступление
	|ИЗ
	|	ВремДатыПоследнееПоступление КАК ВремДатыПоследнееПоступление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремВсеПоследнееПоступление КАК ВремВсеПоследнееПоступление
	|		ПО ВремДатыПоследнееПоступление.Номенклатура = ВремВсеПоследнееПоступление.Номенклатура
	|			И ВремДатыПоследнееПоступление.Период = ВремВсеПоследнееПоступление.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриобретениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ПриобретениеТоваровУслугТовары.Цена) КАК Цена,
	|	МАКСИМУМ(ВремРегистраторПоследнееПоступление.Период) КАК Период,
	|	МАКСИМУМ(ПриобретениеТоваровУслугТовары.Ссылка.Контрагент) КАК Контрагент
	|ПОМЕСТИТЬ ВремИнфоПоследнегоПрихода
	|ИЗ
	|	ВремРегистраторПоследнееПоступление КАК ВремРегистраторПоследнееПоступление
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
	|		ПО ВремРегистраторПоследнееПоступление.Регистратор = ПриобретениеТоваровУслугТовары.Ссылка
	|			И ВремРегистраторПоследнееПоступление.Номенклатура = ПриобретениеТоваровУслугТовары.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриобретениеТоваровУслугТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНоменклатура.Номенклатура КАК Номенклатура,
	|	ОстаткиНоменклатура.Код КАК Код,
	|	ОстаткиНоменклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ОстаткиНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
	|	ОстаткиНоменклатура.КоличествоВсего КАК КоличествоВсего,
	|	ЕСТЬNULL(ВремДнейНаличия.ДнейНаличия, 0) КАК ДнейНаличия,
	|	ЕСТЬNULL(Продажи.КоличествоПродаж, 0) КАК КоличествоПродаж,
	|	ЕСТЬNULL(Продажи.СуммаПродаж, 0) КАК СуммаПродаж,
	|	ЕСТЬNULL(Продажи.КоличествоДокументов, 0) КАК КоличествоДокументов,
	|	ЕСТЬNULL(Продажи.ДатаПоследнейПродажи, 0) КАК ДатаПоследнейПродажи,
	|	ЕСТЬNULL(ВремКоличествоЗакупок.КоличествоЗакупок, 0) КАК КоличествоЗакупок,
	|	ЕСТЬNULL(ВремСебестоимость.Себестоимость, 0) КАК СебестоимостьЗаШтуку,
	|	ЕСТЬNULL(ВремРозничнаяЦена.Цена, 0) КАК РозничнаяЦена,
	|	ЕСТЬNULL(ВремРозничнаяЦена.ДатаРозница, 0) КАК ДатаРозничнаяЦена,
	|	ЕСТЬNULL(ВремЗакупочнаяЦена.Цена, 0) КАК ЗакупочнаяЦена,
	|	ЕСТЬNULL(ВремИнфоПоследнегоПрихода.Цена, 0) КАК ЦенаПоследнейЗакупки,
	|	ЕСТЬNULL(ВремИнфоПоследнегоПрихода.Период, 0) КАК ДатаПоследнейЗакупки,
	|	ЕСТЬNULL(ВремИнфоПоследнегоПрихода.Контрагент, 0) КАК Поставщик
	|ИЗ
	|	ОстаткиНоменклатура КАК ОстаткиНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремДнейНаличия КАК ВремДнейНаличия
	|		ПО ОстаткиНоменклатура.Номенклатура = ВремДнейНаличия.НоменклатураДнейНаличия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Продажи КАК Продажи
	|		ПО ОстаткиНоменклатура.Номенклатура = Продажи.ВыручкаНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремКоличествоЗакупок КАК ВремКоличествоЗакупок
	|		ПО ОстаткиНоменклатура.Номенклатура = ВремКоличествоЗакупок.НоменклатураЗакупки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремСебестоимость КАК ВремСебестоимость
	|		ПО ОстаткиНоменклатура.Номенклатура = ВремСебестоимость.НоменклатураСС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремРозничнаяЦена КАК ВремРозничнаяЦена
	|		ПО ОстаткиНоменклатура.Номенклатура = ВремРозничнаяЦена.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремЗакупочнаяЦена КАК ВремЗакупочнаяЦена
	|		ПО ОстаткиНоменклатура.Номенклатура = ВремЗакупочнаяЦена.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВремИнфоПоследнегоПрихода КАК ВремИнфоПоследнегоПрихода
	|		ПО ОстаткиНоменклатура.Номенклатура = ВремИнфоПоследнегоПрихода.Номенклатура";
	
	УстановитьПривилегированныйРежим(Истина);  
	ПараметрНачалоПериода = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("СтандартныйПериод"));
	СписокСкладов = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("СписокСкладов")); 
	Запрос.УстановитьПараметр("НачалоПериода", ПараметрНачалоПериода.Значение.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ПараметрНачалоПериода.Значение.ДатаОкончания);
	Запрос.УстановитьПараметр("ЗакупочнаяЦена", Справочники.ВидыЦен.НайтиПоНаименованию("Закупочная",Истина));
	Запрос.УстановитьПараметр("РозничнаяЦена", Справочники.ВидыЦен.НайтиПоНаименованию("Розница",Истина));
	Запрос.УстановитьПараметр("Склады", СписокСкладов.Значение);
	НаборДанныхЗапрос = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("НаборДанныхЗапрос",НаборДанныхЗапрос);
	//<--------------------------------------------------------------------------------------->//
	//Инициализируем процессор компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	//Очищаем документ результата
	// ДокументРезультат.Очистить();
	
	//Выводим отчет в документ
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
КонецПроцедуры
