Процедура СоздатьСделку(МассивСсылок) Экспорт
	Вебхук = Константы.Расш6270_БитриксВебхук.Получить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиента.Дата КАК Дата,
		|	ЗаказКлиента.Ссылка КАК Ссылка,
		|	ЗаказКлиента.Номер КАК Номер,
		|	ЗаказКлиента.СуммаДокумента КАК Сумма,
		|	Расш6270_Состояние.Состояние КАК Состояние
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Расш6270_Состояние КАК Расш6270_Состояние
		|		ПО Расш6270_Состояние.Объект = ЗаказКлиента.Ссылка
		|ГДЕ
		|	ЗаказКлиента.Ссылка В(&МассивСсылок)
		|	И Расш6270_Состояние.Состояние ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Ответ = БитриксАпиСоздатьСделку(Вебхук, Выборка.Дата, Выборка.Номер, Выборка.Сумма);
		Если Ответ.Получить("result") <> Неопределено Тогда
			НомерСделки = Ответ["result"];
			НовЗапись = РегистрыСведений.Расш6270_Состояние.СоздатьМенеджерЗаписи();
			НовЗапись.Объект = Выборка.Ссылка;
			НовЗапись.Состояние = XMLСтрока(НомерСделки);
			НовЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция БитриксАпиСоздатьСделку(Вебхук, Дата, Номер, Сумма)
	ПараметрыJSON = Новый Структура;
	Поля = Новый Структура;
	Поля.Вставить("OPPORTUNITY", Сумма);
	Поля.Вставить("TITLE", "Заказ из 1с №" + Номер + " от " + Формат(Дата,"ДФ=dd.MM.yy"));
	Поля.Вставить("UF_CRM_1718035448113", Номер);
	ПараметрыJSON.Вставить("fields", Поля);
	ПараметрыJSON.Вставить("params", Новый Структура("REGISTER_SONET_EVENT", "Y"));
	Результат = Коннектор.PostJson(Вебхук + "crm.deal.add.json", ПараметрыJSON);
	Возврат Результат;
КонецФункции
