Функция ТелефонПоСтандарту(Телефон)
	ИсхСтр = СокрЛП(Телефон);
	РезСтр = "";
	Если ИсхСтр <> "" Тогда
		Для Инд = 1 По СтрДлина(ИсхСтр) Цикл
			Симв = Сред(ИсхСтр, Инд, 1);
			Если Найти("0123456789", Симв) = 0 Тогда
				Продолжить;
			КонецЕсли;
			РезСтр = РезСтр + Симв;
		КонецЦикла;
		
		Если СтрДлина(РезСтр) = 10 Тогда
			РезСтр = "7" + РезСтр;
		КонецЕсли;
		
		Если СтрДлина(РезСтр) = 11 И (Лев(РезСтр, 1) = "8" ИЛИ Лев(РезСтр, 1) = "7") Тогда
			РезСтр = "7" + Сред(РезСтр, 2);
			Возврат РезСтр;
		Иначе
			Сообщить("Неадекватный номер: " + ИсхСтр + " Пытались: " + РезСтр);
			Возврат "";
		КонецЕсли;
	КонецЕсли;
	Возврат ""
КонецФункции

Процедура СоздатьСделку(МассивСсылок) Экспорт
	Вебхук = Константы.Расш6270_БитриксВебхук.Получить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказКлиента.Дата КАК Дата,
		|	ЗаказКлиента.Ссылка КАК Ссылка,
		|	ЗаказКлиента.Номер КАК Номер,
		|	ЗаказКлиента.СуммаДокумента КАК Сумма,
		|	Расш6270_Состояние.Состояние КАК Состояние,
		|	ЗаказКлиента.Комментарий КАК Комментарий
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Расш6270_Состояние КАК Расш6270_Состояние
		|		ПО (Расш6270_Состояние.Объект = ЗаказКлиента.Ссылка)
		|ГДЕ
		|	ЗаказКлиента.Ссылка В(&МассивСсылок)
		|	И Расш6270_Состояние.Состояние ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		// Поищем клиента в битрикс
		ИдКлиента = "";
		РеквизитТелефон = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "Телефон_a075aff4e1eb4736bb68d2d8a0fdb92e");
		Телефон = УправлениеСвойствами.ЗначениеСвойства(Выборка.Ссылка, РеквизитТелефон);
		Телефон = ТелефонПоСтандарту(Телефон);
		
		Если НЕ ПустаяСтрока(Телефон) Тогда
			ИдКлиента = БитриксАпиНайтиКлиентаПоТелефону(Вебхук, Телефон);
		КонецЕсли;
		
		РеквизитИмя = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ИмяКлиента_876834a602e143b28fa4fcf8b2e57e3b");
		Имя = УправлениеСвойствами.ЗначениеСвойства(Выборка.Ссылка, РеквизитИмя);
		
		Ответ = БитриксАпиСоздатьСделку(Вебхук, Выборка.Дата, Выборка.Номер, Выборка.Сумма, Выборка.Комментарий, Имя, Телефон, ИдКлиента);
		Если Ответ.Получить("result") <> Неопределено Тогда
			НомерСделки = Ответ["result"];
			НовЗапись = РегистрыСведений.Расш6270_Состояние.СоздатьМенеджерЗаписи();
			НовЗапись.Объект = Выборка.Ссылка;
			НовЗапись.Состояние = XMLСтрока(НомерСделки);
			НовЗапись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция БитриксАпиНайтиКлиентаПоТелефону(Вебхук, Телефон)
	ПараметрыJSON = Новый Структура;
	Поля = Новый Массив;
	Поля.Добавить("ID");
	ПараметрыJSON.Вставить("select", Поля);
	ПараметрыJSON.Вставить("filter", Новый Структура("PHONE", Телефон));
	Ответ = Коннектор.PostJson(Вебхук + "crm.contact.list.json", ПараметрыJSON);
	Если Ответ.Получить("total") > 0 Тогда
		Результат = Ответ.Получить("result");
		Возврат Результат[0].Получить("ID");
	КонецЕсли;
	Возврат "";
КонецФункции

Функция БитриксАпиСоздатьСделку(Вебхук, Дата, Номер, Сумма, Знач Комментарий, Имя = "", Телефон = "", ИдКлиента = "")
	ПараметрыJSON = Новый Структура;
	Поля = Новый Структура;
	Поля.Вставить("OPPORTUNITY", Сумма);
	Поля.Вставить("TITLE", "Заказ из 1с №" + Номер + " от " + Формат(Дата,"ДФ=dd.MM.yy"));
	Поля.Вставить("UF_CRM_1718035448113", Номер);
	Если ПустаяСтрока(ИдКлиента) Тогда
		Комментарий = "(" + Имя + ", " + Телефон + ") " + Комментарий;
	Иначе
		Поля.Вставить("CONTACT_IDS", Новый Структура("ID", ИдКлиента));
	КонецЕсли;
	Поля.Вставить("COMMENTS", Комментарий);
	ПараметрыJSON.Вставить("fields", Поля);
	ПараметрыJSON.Вставить("params", Новый Структура("REGISTER_SONET_EVENT", "Y"));
	Результат = Коннектор.PostJson(Вебхук + "crm.deal.add.json", ПараметрыJSON);
	Возврат Результат;
КонецФункции
