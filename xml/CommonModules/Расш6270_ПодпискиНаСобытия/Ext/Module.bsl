Процедура Расш6270_ДокументыОбработкаПроведенияОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	// Вставить содержимое обработчика.
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.УстановкаЦенНоменклатуры") Тогда
		НоваяЗапись = РегистрыСведений.Расш6270_ОтложенныеЗадания.СоздатьМенеджерЗаписи();
		НоваяЗапись.Объект = Источник.Ссылка;
		НоваяЗапись.Событие = "УведомитьЯндексКарты";
		НоваяЗапись.Записать();
		
		НоваяЗапись = РегистрыСведений.Расш6270_ОтложенныеЗадания.СоздатьМенеджерЗаписи();
		НоваяЗапись.Объект = Источник.Ссылка;
		НоваяЗапись.Событие = "УведомитьРозница";
		НоваяЗапись.Записать();
		
		Если Источник.ДокументОснование <> Неопределено Тогда
			НоваяЗапись = РегистрыСведений.Расш6270_Состояние.СоздатьМенеджерЗаписи();
			НоваяЗапись.Объект = Источник.ДокументОснование;
			НоваяЗапись.Булево = Истина;
			НоваяЗапись.Записать();
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры 

Процедура Расш6270_ОбработкаУведомлений() Экспорт
	НаборЗаписей = РегистрыСведений.Расш6270_ОтложенныеЗадания.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Запись.Событие = "УведомитьЯндексКарты" Тогда
			УведомитьОбИзмененииЗакупочныхЦенДляТоваровЯндексМаркета(Запись.Объект);
			
		ИначеЕсли  Запись.Событие = "УведомитьРозница" Тогда
			
			// Потом перепишу уведомление пока оно в модуле установки цен номенклатуры
			
		КонецЕсли;
	КонецЦикла; 
	НаборЗаписей.Очистить(); 
	НаборЗаписей.Записать();

КонецПроцедуры  

Процедура УведомитьОбИзмененииЗакупочныхЦенДляТоваровЯндексМаркета(ДокУстановкаЦен)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпрНоменклатура.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_НоменклатураЯндекс
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|ГДЕ
		|	СпрНоменклатура.Расш6270_ShopSKU <> """"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УстановкаЦенНоменклатурыТовары.Номенклатура.Расш6270_ShopSKU КАК НоменклатураРасш6270_ShopSKU,
		|	УстановкаЦенНоменклатурыТовары.Номенклатура КАК Номенклатура,
		|	УстановкаЦенНоменклатурыТовары.Цена КАК Цена,
		|	УстановкаЦенНоменклатурыТовары.Номенклатура.Наименование КАК НоменклатураНаименование
		|ПОМЕСТИТЬ ВТ_НоменклатураИзУстановкиЦен
		|ИЗ
		|	Документ.УстановкаЦенНоменклатуры.Товары КАК УстановкаЦенНоменклатурыТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НоменклатураЯндекс КАК ВТ_НоменклатураЯндекс
		|		ПО (ВТ_НоменклатураЯндекс.Ссылка = УстановкаЦенНоменклатурыТовары.Номенклатура)
		|ГДЕ
		|	УстановкаЦенНоменклатурыТовары.ВидЦены = &ВидЦены
		|	И УстановкаЦенНоменклатурыТовары.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
		|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
		|ПОМЕСТИТЬ ВТ_ЦенаСтарая
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|			&ДатаДоПроведения,
		|			(ВидЦены, Номенклатура) В
		|				(ВЫБРАТЬ
		|					&ВидЦены,
		|					ВТ_НоменклатураИзУстановкиЦен.Номенклатура
		|				ИЗ
		|					ВТ_НоменклатураИзУстановкиЦен)) КАК ЦеныНоменклатурыСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		
		|	ВТ_НоменклатураИзУстановкиЦен.НоменклатураРасш6270_ShopSKU КАК ShopSKU,
		|	ВТ_НоменклатураИзУстановкиЦен.Номенклатура КАК Номенклатура,
		|	ЕСТЬNULL(ВТ_ЦенаСтарая.Цена, 0) КАК ЦенаСтарая,
		|	ВТ_НоменклатураИзУстановкиЦен.Цена КАК Цена
		|ИЗ
		|	ВТ_НоменклатураИзУстановкиЦен КАК ВТ_НоменклатураИзУстановкиЦен
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЦенаСтарая КАК ВТ_ЦенаСтарая
		|		ПО ВТ_НоменклатураИзУстановкиЦен.Номенклатура = ВТ_ЦенаСтарая.Номенклатура";
	
	ВидЦены = Справочники.ВидыЦен.НайтиПоНаименованию("Закупочная");
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	Запрос.УстановитьПараметр("Ссылка", ДокУстановкаЦен);
	Запрос.УстановитьПараметр("ДатаДоПроведения", Новый Граница (ДокУстановкаЦен.Дата, ВидГраницы.Исключая));
	
	Выборка = Запрос.Выполнить();
	Если Выборка.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент;
	Построитель = Новый ПостроительОтчета;
	Построитель.ИсточникДанных =  Новый ОписаниеИсточникаДанных(Выборка);
	Построитель.Выполнить();
	Построитель.Вывести(ТабДок); 
	
	ВремФайл = ПолучитьИмяВременногоФайла(".xlsx");
	ТабДок.Записать(ВремФайл, ТипФайлаТабличногоДокумента.XLSX);
	
	Расш6270_Действия.ОтправитьДокумент(Константы.Расш6270_ТокенТелеграм.Получить(),
										Константы.Расш6270_ЧатЯндексМаркета.Получить(),
										Строка(ДокУстановкаЦен),
										ВремФайл); 
										
	Попытка УдалитьФайлы(ВремФайл);
	Исключение
	КонецПопытки;
	
	
КонецПроцедуры

Процедура Расш6270_СправочникиПриКопированииПриКопировании(Источник, ОбъектКопирования) Экспорт
	// Вставить содержимое обработчика.
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Номенклатура") Тогда
		СтеретьПриКопировании = Новый Структура;
		СтеретьПриКопировании.Вставить("Расш6270_ОсновнаяМодификация", 	Справочники.Номенклатура.ПустаяСсылка());
		СтеретьПриКопировании.Вставить("Расш6270_Недействителен", 		Ложь);
		СтеретьПриКопировании.Вставить("Расш6270_ВыгружатьНаСайт", 		Ложь);
		СтеретьПриКопировании.Вставить("Расш6270_ВыгружатьДляЯндекса", 	Ложь);
		СтеретьПриКопировании.Вставить("Расш6270_СтатусТовара", 	Ложь);
		СтеретьПриКопировании.Вставить("Расш6270_СезонныйТовар", 	Ложь);
		СтеретьПриКопировании.Вставить("Расш6270_ОсновнойПоставщик", 	Справочники.Контрагенты.ПустаяСсылка());
		СтеретьПриКопировании.Вставить("Расш6270_ShopSKU",				"");
		ЗаполнитьЗначенияСвойств(Источник, СтеретьПриКопировании);
	КонецЕсли;	
КонецПроцедуры

Процедура Расш6270_ДокументыПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	// Вставить содержимое обработчика.
	Если РольДоступна("ПолныеПрава") Тогда // Проверить на полные права
		Возврат;
	КонецЕсли;
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЗаказКлиента") 
		ИЛИ ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваровУслуг") 
		ИЛИ ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровОтКлиента") Тогда
		УстановитьПривилегированныйРежим(Истина);
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Расш6270_ДоступыПользователей.ОбъектДоступа КАК Ссылка
			|ИЗ
			|	РегистрСведений.Расш6270_ДоступыПользователей КАК Расш6270_ДоступыПользователей
			|ГДЕ
			|	Расш6270_ДоступыПользователей.Пользователь = &Пользователь
			|	И (ВЫРАЗИТЬ(Расш6270_ДоступыПользователей.ОбъектДоступа КАК Справочник.Склады)) = &Склад";
		
		Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
		Запрос.УстановитьПараметр("Склад", Источник.Склад);
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Нельзя использовать выбранный склад'");
			Сообщение.Сообщить();
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПриходныйКассовыйОрдер")
				ИЛИ ТипЗнч(Источник) = Тип("ДокументОбъект.РасходныйКассовыйОрдер") Тогда
		УстановитьПривилегированныйРежим(Истина);
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Расш6270_ДоступыПользователей.ОбъектДоступа КАК Ссылка
			|ИЗ
			|	РегистрСведений.Расш6270_ДоступыПользователей КАК Расш6270_ДоступыПользователей
			|ГДЕ
			|	Расш6270_ДоступыПользователей.Пользователь = &Пользователь
			|	И (ВЫРАЗИТЬ(Расш6270_ДоступыПользователей.ОбъектДоступа КАК Справочник.Кассы)) = &Касса";
		
		Запрос.УстановитьПараметр("Пользователь", ПараметрыСеанса.ТекущийПользователь);
		Запрос.УстановитьПараметр("Касса", Источник.Касса);
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Нельзя использовать выбранную кассу'");
			Сообщение.Сообщить();
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ОперацияПоПлатежнойКарте") Тогда
		УстановитьПривилегированныйРежим(Истина);
		Если ЗначениеЗаполнено(Источник.ДоговорЭквайринга) Тогда
			Если Источник.ДоговорЭквайринга.СпособПроведенияПлатежа = Перечисления.СпособыПроведенияПлатежей.ЭквайринговыйТерминал Тогда
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	НастройкиРМКЭквайринговыеТерминалы.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал
				|ИЗ
				|	Справочник.НастройкиРМК.ЭквайринговыеТерминалы КАК НастройкиРМКЭквайринговыеТерминалы
				|ГДЕ
				|	НастройкиРМКЭквайринговыеТерминалы.Ссылка.РабочееМесто = &РабочееМесто
				|	И НастройкиРМКЭквайринговыеТерминалы.ЭквайринговыйТерминал = &ЭквайринговыйТерминал";
				
				Запрос.УстановитьПараметр("РабочееМесто", ПараметрыСеанса.РабочееМестоКлиента);
				Запрос.УстановитьПараметр("ЭквайринговыйТерминал", Источник.ЭквайринговыйТерминал);
				РезультатЗапроса = Запрос.Выполнить();
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				Если ВыборкаДетальныеЗаписи.Количество() = 0 Тогда
					Отказ = Истина;
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = НСтр("ru = 'Нельзя использовать выбранный эквайринговый терминал'");
					Сообщение.Сообщить();
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;	
КонецПроцедуры 