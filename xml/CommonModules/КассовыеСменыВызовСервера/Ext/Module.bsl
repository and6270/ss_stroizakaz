
&Вместо("ОписаниеПоследнейКассовойСмены")
Функция Расш6270_ОписаниеПоследнейКассовойСмены(ФискальноеУстройство)
	// Вставить содержимое метода. 
	//Черных. Начало.
	//
	//1. Сначала определяем кассу ККМ по фискальному устройству.
	//2. Затем по кассе ККМ - определим кассовую смену.
	//Таким образом, если мы в настройках торгового оборудования к одной КассеККМ (справочник.КассыККМ)
	//привяжем несколько рабочих мест, то они смогут печатать чеки, используя одну и ту же кассовую смену
	//Шаг 1. Определение кассы ККМ по фискальному устройству.
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиРМККассыККМ.КассаККМ КАК КассаККМ
	|ИЗ
	|	Справочник.НастройкиРМК.КассыККМ КАК НастройкиРМККассыККМ
	|ГДЕ
	|	НастройкиРМККассыККМ.ПодключаемоеОборудование = &ПодключаемоеОборудование";
	Запрос.УстановитьПараметр("ПодключаемоеОборудование", ФискальноеУстройство);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		КассаККМ = Выборка.КассаККМ;
		Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
		//Такое может быть только при некорректной настройке. Не выбрана касса ККМ в регистре торгового оборудования
			возврат Неопределено;
		КонецЕсли; 
		//Шаг 2. Определяем кассовую смену по кассе ККМ
		Запрос = Новый Запрос();
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|   КассоваяСмена.Ссылка КАК КассоваяСмена,
		|   КассоваяСмена.ФискальноеУстройство,
		|   КассоваяСмена.НачалоКассовойСмены,
		|   КассоваяСмена.ОкончаниеКассовойСмены,
		|   ДОБАВИТЬКДАТЕ(КассоваяСмена.НачалоКассовойСмены, ДЕНЬ, 1) КАК ДатаИстеченияСрокаДействия,
		|   КассоваяСмена.Организация,
		|   КассоваяСмена.Статус,
		|   КассоваяСмена.ДатаСменыККТ,
		|   КассоваяСмена.НомерСменыККТ,
		|   КассоваяСмена.КассаККМ
		|   ИЗ
		|   Документ.КассоваяСмена КАК КассоваяСмена
		|   ГДЕ
		|   КассоваяСмена.КассаККМ = &КассаККМ
		|   И КассоваяСмена.Проведен
		|   УПОРЯДОЧИТЬ ПО
		|   КассоваяСмена.НачалоКассовойСмены УБЫВ";
		Запрос.УстановитьПараметр("КассаККМ", КассаККМ);
		Результат = Запрос.Выполнить().Выгрузить();
		Если Результат.Количество() = 0 Тогда
			Возврат Неопределено;
		Иначе
			СтруктураРезультат = Новый Структура();
			Для Каждого КолонкаРезультата Из Результат.Колонки Цикл
				СтруктураРезультат.Вставить(КолонкаРезультата.Имя, Результат[0][КолонкаРезультата.Имя]);
			КонецЦикла;
			Возврат СтруктураРезультат;
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
 //Черных. Конец
КонецФункции
