
&После("ОбработкаДополнительнойРасшифровки")
Процедура Расш6270_ОбработкаДополнительнойРасшифровки(ФормаОтчета, Элемент, Расшифровка, СтандартнаяОбработка)
	Если ФормаОтчета.ИмяФормы = "Отчет.Расш6270_ОтчетТовароведа.Форма"
		ИЛИ ФормаОтчета.ИмяФормы = "Отчет.Расш6270_ОтчетСнабжение2024.Форма" 
	Тогда
		СтандартнаяОбработка = Ложь;
		ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(ФормаОтчета.НастройкиОтчета.АдресСхемы);
		ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ФормаОтчета.ОтчетДанныеРасшифровки, ИсточникНастроек);
		
		ДополнительныеДействия = Новый СписокЗначений;
		ДополнительныеДействия.Добавить("ОткрытьОтчетЗакупки", "Показать закупки",,БиблиотекаКартинок.АктивироватьЗадачу);
		ДополнительныеДействия.Добавить("ОткрытьОтчетРезервы", "Показать резервы",,БиблиотекаКартинок.ДиалогИнформация);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДанныеРасшифровки", ФормаОтчета.ОтчетДанныеРасшифровки);
		ДополнительныеПараметры.Вставить("Расшифровка", Расшифровка);
		
		ЧтоДелатьПослеВыбора = Новый ОписаниеОповещения("Расш6270_ОбработкаДополнительнойРасшифровкиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ОбработкаРасшифровки.ПоказатьВыборДействия(ЧтоДелатьПослеВыбора, Расшифровка, ,ДополнительныеДействия);
	КонецЕсли;
КонецПроцедуры

Процедура Расш6270_ОбработкаДополнительнойРасшифровкиЗавершение(ВыбранноеДействие, ПараметрВыбранногоДействия, ДополнительныеПараметры) Экспорт 
	Если ВыбранноеДействие = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Номенклатура = Неопределено;
	Отчет = "";
	Если ВыбранноеДействие = "ОткрытьОтчетЗакупки" Тогда
		Номенклатура = Расш6270_Отчеты.ПолучитьЗначениеПоляРасшифровки(ДополнительныеПараметры.ДанныеРасшифровки, ДополнительныеПараметры.Расшифровка);
		Отчет = "Отчет.Расш6270_ОтчетПоследниеЗакупки.ФормаОбъекта";
	ИначеЕсли ВыбранноеДействие = "ОткрытьОтчетРезервы" Тогда
		Номенклатура = Расш6270_Отчеты.ПолучитьЗначениеПоляРасшифровки(ДополнительныеПараметры.ДанныеРасшифровки, ДополнительныеПараметры.Расшифровка);
		Отчет = "Отчет.Расш6270_ОтчетРезервыТоваров.ФормаОбъекта";
	КонецЕсли;
	
	Если Номенклатура <> Неопределено Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Номенклатура", Номенклатура));
		
		ОткрытьФорму(Отчет, ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры
