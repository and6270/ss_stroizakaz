
&НаКлиенте
Процедура Расш6270_ВыгрузитьНаСайтПосле(Команда)
	ВыполнитьОбменССайтомКлиент();
	ПоказатьОповещениеПользователя("Данные успешно выгружены");
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура Расш6270_ВыгрузитьНаСайтСНаименованиемПосле(Команда)
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект);
	
	ПоказатьВопрос(Оповещение,
		"Наименование можно выгружать только для новых товаров! Выгружать на сайт?",
		РежимДиалогаВопрос.ДаНет,
		0,
		КодВозвратаДиалога.Нет,
		"Одумайтесь!"
	);
КонецПроцедуры  

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыполнитьОбменССайтомКлиент(Истина);
		ПоказатьОповещениеПользователя("Данные успешно выгружены");
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбменССайтомКлиент(ВыгружатьНаименованиеНоменклатуры = Ложь)
	МассивДокументовУЦН = ЭтотОбъект.ТекущийЭлемент.ВыделенныеСтроки;
	ТекстCSV = Расш6270_МегаГрупп.СоздатьТекстCSV(МассивДокументовУЦН, ВыгружатьНаименованиеНоменклатуры);
	Результат = Расш6270_МегаГрупп.ВыполнитьОбменССайтомКлиент(ТекстCSV);
	Если Результат Тогда
		КомментарииДокументов = ПолучитьКомментарии(МассивДокументовУЦН);
		ЧатИД = Константы.Расш6270_ЧатСтройЗаказ.Получить();
		ТокенТелеграмм = Константы.Расш6270_ТокенТелеграм.Получить();
		НомераДокУстановкиЦен = ПолучитьСтрокуНомеровДокументов(МассивДокументовУЦН);
		ТекстСообщенияТГ = "Выгружены на сайт цены по документам №: " + НомераДокУстановкиЦен + ", "+ ПользователиКлиентСервер.ТекущийПользователь() + Символы.ПС + КомментарииДокументов;
		Расш6270_Действия.ОтправитьТекстовоеСообщение(ТокенТелеграмм, ЧатИД, ТекстСообщенияТГ);
		// добавление записи в регистр сведений
		Для Каждого док Из МассивДокументовУЦН Цикл
			Запись = РегистрыСведений.Расш6270_Состояние.СоздатьМенеджерЗаписи();
			Запись.Объект = док;
			Запись.Состояние = "Выгружено, " + ПользователиКлиентСервер.ТекущийПользователь() + ", " + ТекущаяДата();
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ПолучитьКомментарии(МассивДокументов)
	СтрокаКомментариев = "";
	Для Каждого Док из МассивДокументов Цикл
		Если Док.Комментарий <> "" Тогда
			СтрокаКомментариев = СтрокаКомментариев + Док.Комментарий + ", "; 
		КонецЕсли;
	КонецЦикла;
	Возврат СтрокаКомментариев;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтрокуНомеровДокументов(МассивДок)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УстановкаЦенНоменклатуры.Номер КАК Номер
		|ИЗ
		|	Документ.УстановкаЦенНоменклатуры КАК УстановкаЦенНоменклатуры
		|ГДЕ
		|	УстановкаЦенНоменклатуры.Ссылка В(&МассивСсылок)";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивДок);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Результат = "";
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДетальныеЗаписи.Номер, Истина, Истина);
		Результат = ?(ПустаяСтрока(Результат), Номер, Результат + ", " + Номер); 	
	КонецЦикла;

	Возврат Результат;
КонецФункции


&НаКлиенте
Процедура Расш6270_СохранитьCSVПосле(Команда)
	ДопПараметры = ЭтотОбъект.ТекущийЭлемент.ВыделенныеСтроки;
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораФайла", ЭтотОбъект, ДопПараметры);
	ДиалогСохранение = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение); 
	ДиалогСохранение.Фильтр = "Табличный документ (.csv)|*.csv";
	ДиалогСохранение.Расширение = "csv"; 
	ДиалогСохранение.ПолноеИмяФайла = "Price";
	ДиалогСохранение.Заголовок = "Выберите сохранение файла";
	ДиалогСохранение.Показать(Оповещение);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы <> Неопределено Тогда
		ИмяФайла = ВыбранныеФайлы[0];
		ТекстФайла = СформироватьТекстНасервере(ДополнительныеПараметры);
		ТекДокумент = Новый ТекстовыйДокумент;
		ТекДокумент.УстановитьТекст(ТекстФайла);            
		Попытка
			ТекДокумент.Записать(ИмяФайла,КодировкаТекста.UTF8); 
			ПоказатьОповещениеПользователя("Файл сохранен");
			
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не удалось записать файл, описание ошибки: " + ОписаниеОшибки();
			Сообщение.Сообщить();
		КонецПопытки;
    КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьТекстНасервере(МассивДокумнтов) 
	ТекстCSV = Расш6270_МегаГрупп.СоздатьТекстCSV(МассивДокумнтов, Ложь);	
	Возврат ТекстCSV;
КонецФункции

&НаСервере
Процедура Расш6270_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	ТекстЗапроса = "ВЫБРАТЬ
	|	ДокументУстановкаЦенНоменклатуры.Ссылка КАК Ссылка,
	|	ДокументУстановкаЦенНоменклатуры.Статус КАК Статус,
	|	ДокументУстановкаЦенНоменклатуры.Проведен КАК Проведен,
	|	ВЫБОР
	|		КОГДА ДокументУстановкаЦенНоменклатуры.Проведен
	|				И ДокументУстановкаЦенНоменклатуры.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыУстановокЦенНоменклатуры.Согласован)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияУстановокЦенНоменклатуры.Действует)
	|		КОГДА ДокументУстановкаЦенНоменклатуры.Проведен
	|				И ДокументУстановкаЦенНоменклатуры.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыУстановокЦенНоменклатуры.НеСогласован)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияУстановокЦенНоменклатуры.ОжидаетсяСогласование)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияУстановокЦенНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Состояние,
	|	ДокументУстановкаЦенНоменклатуры.Номер КАК Номер,
	|	ДокументУстановкаЦенНоменклатуры.Дата КАК Дата,
	|	РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДокументУстановкаЦенНоменклатуры.Дата, ДЕНЬ), ДокументУстановкаЦенНоменклатуры.Дата, СЕКУНДА) + 1 КАК НомерВПределахДня,
	|	ДокументУстановкаЦенНоменклатуры.Ответственный КАК Ответственный,
	|	ДокументУстановкаЦенНоменклатуры.Комментарий КАК Комментарий,
	|	ВЫБОР
	|		КОГДА НЕ ДокументУстановкаЦенНоменклатуры.Проведен
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		КОГДА ДокументУстановкаЦенНоменклатуры.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыУстановокЦенНоменклатуры.НеСогласован)
	|			ТОГДА ДокументУстановкаЦенНоменклатуры.Дата
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|	КОНЕЦ КАК ДатаСобытия,
	|	ВЫБОР
	|		КОГДА НЕ ДокументУстановкаЦенНоменклатуры.Проведен
	|			ТОГДА ЛОЖЬ
	|		КОГДА ДокументУстановкаЦенНоменклатуры.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыУстановокЦенНоменклатуры.НеСогласован)
	|				И ДокументУстановкаЦенНоменклатуры.Дата < &ДатаАктуальности
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Просрочен,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК ВидыЦен,
	|	ДокументУстановкаЦенНоменклатуры.МаркетинговоеМероприятие КАК МаркетинговоеМероприятие,
	|	Расш6270_Состояние.Состояние КАК СостояниеОбмена
	|ИЗ
	|	Документ.УстановкаЦенНоменклатуры КАК ДокументУстановкаЦенНоменклатуры
	|		{ЛЕВОЕ СОЕДИНЕНИЕ Документ.УстановкаЦенНоменклатуры.ВидыЦен КАК УстановкаЦенНоменклатурыВидыЦен
	|		ПО (УстановкаЦенНоменклатурыВидыЦен.Ссылка = ДокументУстановкаЦенНоменклатуры.Ссылка)}
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Расш6270_Состояние КАК Расш6270_Состояние
	|		ПО (ДокументУстановкаЦенНоменклатуры.Ссылка = Расш6270_Состояние.Объект)
	|{ГДЕ
	|	УстановкаЦенНоменклатурыВидыЦен.ВидЦены КАК ВидЦены}";
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = "Документ.УстановкаЦенНоменклатуры";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	НовыйЭлемент = Элементы.Добавить("СтатусОбмена", Тип("ПолеФормы"), Элементы.Список);
	НовыйЭлемент.ПутьКДанным		= "Список.СостояниеОбмена";
	НовыйЭлемент.Вид				= ВидПоляФормы.ПолеНадписи;
	НовыйЭлемент.ТолькоПросмотр		= Истина;
	НовыйЭлемент.Заголовок			= "Состояние обмена";
	
КонецПроцедуры

