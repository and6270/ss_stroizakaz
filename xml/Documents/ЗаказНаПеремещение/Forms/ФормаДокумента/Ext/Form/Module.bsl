
&НаСервере
Процедура Расш6270_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	//Создаем Команду
	НоваяКоманда = ЭтаФорма.Команды.Добавить("Расш6270_ЗагрузитьИзВнешнегоФайла");
	НоваяКоманда.Действие = "ЗагрузитьИзВнешнегоФайла";
	НоваяКоманда.Заголовок = "Загрузить из внешнего файла";
	
	НоваяКнопка = ЭтаФорма.Элементы.Добавить("Расш6270_ЗагрузитьИзВнешнегоФайла", Тип("КнопкаФормы"), ЭтаФорма.Элементы.ГруппаЗаполнить);
	НоваяКнопка.ИмяКоманды = "Расш6270_ЗагрузитьИзВнешнегоФайла";
	НоваяКнопка.Картинка = БиблиотекаКартинок.ЗагрузкаИзВнешнегоИсточника;
	НоваяКнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзВнешнегоФайла(Команда)
	
	ПараметрыЗагрузки = РаботаСТабличнымиЧастямиКлиент.ПараметрыЗагрузкиНоменклатуры();
	ПараметрыЗагрузки.Организация = Объект.Организация;
	ПараметрыЗагрузки.ЗагружатьЦены = Ложь;
	ПараметрыЗагрузки.ДатаЗаполнения = Объект.Дата;
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьИзВнешнегоФайлаЗавершение", ЭтотОбъект);
	РаботаСТабличнымиЧастямиКлиент.ПоказатьФормуЗагрузкиНоменклатуры(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзВнешнегоФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт	
	Если ЗначениеЗаполнено(АдресЗагруженныхДанных) Тогда
		ОчиститьСообщения();
		ПолучитьЗагруженныеТоварыИзХранилища(АдресЗагруженныхДанных);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗагруженныеТоварыИзХранилища(АдресТоваровВХранилище)

	ТоварыИзХранилища = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище);
	
	ПараметрыОтбора = Новый Соответствие;
	ПараметрыОтбора.Вставить("Номенклатура.ТипНоменклатуры", НоменклатураКлиентСервер.ОтборПоТоваруМногооборотнойТаре(Ложь));
	
	Колонки = "Номенклатура,Характеристика,Упаковка,КоличествоУпаковок,НачалоОтгрузки, НоменклатураНабора, ХарактеристикаНабора";
	
	СтруктураДействий = Новый Структура;
	ДобавитьДействияОбеспеченияНаСервере(СтруктураДействий, "ВариантОбеспечения,СкладОбязателен,ДатаОтгрузкиОбязательна");
	ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий);
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Для каждого СтрокаТовара Из ТоварыИзХранилища Цикл
		
		ТекущаяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара,,"КодСтроки");
		ТекущаяСтрока.ИндексНабора = ?(ЗначениеЗаполнено(ТекущаяСтрока.НоменклатураНабора), 1, 0);
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	ОбновитьКолонкуДоступноСервер();
	
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
	МногооборотнаяТараСервер.ОбновитьСостояниеЗаполненияМногооборотнойТары(Объект.СостояниеЗаполненияМногооборотнойТары);
	
КонецПроцедуры