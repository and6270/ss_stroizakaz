
&НаСервереБезКонтекста
Функция ПолучитьОстатокБонусныхБаллов(Партнер)
	Результат = 0;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БонусныеБаллыОстатки.НачисленоОстаток КАК ОстатокБонусов
		|ИЗ
		|	РегистрНакопления.БонусныеБаллы.Остатки(
		|			,
		|			БонуснаяПрограммаЛояльности = &БонуснаяПрограмма
		|				И Партнер = &Партнер) КАК БонусныеБаллыОстатки";
	
	БонуснаяПрограмма = Справочники.БонусныеПрограммыЛояльности.НайтиПоНаименованию("Партнеры");
	Запрос.УстановитьПараметр("БонуснаяПрограмма", БонуснаяПрограмма);
	Запрос.УстановитьПараметр("Партнер", Партнер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат = ВыборкаДетальныеЗаписи.ОстатокБонусов;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура Списать(Команда)
	Если Сумма <  0 ИЛИ Сумма >  ДоступноКСписанию Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Сумма должна быть больше 0 и меньше " + ДоступноКСписанию ;
		Сообщение.Поле = "Сумма";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Партнер.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Выберите партнера'");
		Сообщение.Поле = "Партнер";
		Сообщение.Сообщить();
		Возврат;
		
	КонецЕсли;
	
	Если Касса.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Выберите кассу'");
		Сообщение.Поле = "Касса";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	СписатьНаСервере();
	ДоступноКСписанию = ПолучитьОстатокБонусныхБаллов(Партнер);
	ПоказатьОповещениеПользователя("Операция завершена");
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура СписатьНаСервере()
	НачатьТранзакцию();
	УстановитьПривилегированныйРежим(Истина);
	Попытка
		Пользователь = Пользователи.ТекущийПользователь();
		// 1 Создать документ списание бонусных балов
		ДокСписания = Документы.НачислениеИСписаниеБонусныхБаллов.СоздатьДокумент();
		БонуснаяПрограмма = Справочники.БонусныеПрограммыЛояльности.НайтиПоНаименованию("Партнеры");
		ДокСписания.БонуснаяПрограммаЛояльности = БонуснаяПрограмма;
		ДокСписания.Комментарий = "Создан обработкой, пользователь " + Пользователь;
		ДокСписания.ПериодДействия = Перечисления.Периодичность.Месяц;
		ДокСписания.ПериодОтсрочкиНачалаДействия = Перечисления.Периодичность.Неделя;
		ДокСписания.ВидПравила = Перечисления.ВидыПравилНачисленияБонусныхБаллов.Списание;
		ДокСписания.Дата = ТекущаяДатаСеанса();
		
		НоваяСтрока = ДокСписания.Списание.Добавить();
		НоваяСтрока.Партнер = Партнер;
		НоваяСтрока.Баллы = Сумма;

		ДокСписания.Записать(РежимЗаписиДокумента.Проведение);
		
		// 2 Создать документ списания ден.средств из кассы
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Контрагенты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.Партнер = &Партнер";
		
		Запрос.УстановитьПараметр("Партнер", Партнер);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Контрагент = ВыборкаДетальныеЗаписи.Ссылка;
		КонецЕсли;
		
		ДокСписанияДС = Документы.РасходныйКассовыйОрдер.СоздатьДокумент();
		ДокСписанияДС.Заполнить(Неопределено);
		ДокСписанияДС.Дата = ТекущаяДатаСеанса();
		ДокСписанияДС.Организация = Касса.Владелец;
		ДокСписанияДС.Касса = Касса; 
		ДокСписанияДС.СуммаДокумента = Сумма;
		ДокСписанияДС.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств;
		ДокСписанияДС.Выдать = Строка(Партнер);
		ДокСписанияДС.Контрагент = Контрагент;
		ДокСписанияДС.Комментарий = "Создан обработкой, пользователь " + Пользователь;
		
		НовСтрока = ДокСписанияДС.РасшифровкаПлатежа.Добавить();
		НовСтрока.СтатьяДвиженияДенежныхСредств = Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоКоду("УТ-000021");
		НовСтрока.Сумма = Сумма;
		НовСтрока.ВалютаВзаиморасчетов = Справочники.Валюты.НайтиПоКоду(643);
		НовСтрока.СуммаВзаиморасчетов = Сумма;
		НовСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.НайтиПоНаименованию("Бонусная программа");
		НовСтрока.Организация = Касса.Владелец;
		НовСтрока.КурсЧислительВзаиморасчетов = 1;
		НовСтрока.КурсЗнаменательВзаиморасчетов = 1;
		
		ДокСписанияДС.Записать(РежимЗаписиДокумента.Проведение);
		// 3.Проверить остаток бонусов из зафиксировать транзакцию
		Если ПолучитьОстатокБонусныхБаллов(Партнер) < 0 Тогда
			ВызватьИсключение("У партнера не хватает бонусных балов");
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Сообщить("Ошибка списания бонусных баллов" + ОписаниеОшибки());
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	ДоступноКСписанию = ПолучитьОстатокБонусныхБаллов(Партнер);
КонецПроцедуры
