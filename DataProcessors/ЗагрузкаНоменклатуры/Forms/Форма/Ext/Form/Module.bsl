
&НаКлиенте
Процедура НачатьСопостовление(Команда)
	КоличествоСтрок = ТаблицаДляЗаполнения.ВысотаТаблицы - 1;
	Если НЕ ВидНоменклатуры.Пустая() И КоличествоСтрок Тогда
		МассивСтруктурНовыхДанных = НачатьСопостовлениеНаСервере();
		Если МассивСтруктурНовыхДанных = Неопределено Тогда
			ПоказатьОповещениеПользователя("Для загрузки данных необходимо исправить ошибки, (выделены красным)");	
			Возврат;	
		КонецЕсли;
		
	Иначе 
		ПоказатьОповещениеПользователя("Необходимо выбрать Вид номенклатуры и заполнить табличную часть");
		Возврат;
	КонецЕсли;
	
	ОповещениеВопрос = Новый ОписаниеОповещения("ПослеПоказаВопросаОСоздании", ЭтотОбъект, МассивСтруктурНовыхДанных); 
	ПоказатьВопрос(ОповещениеВопрос, "Ошибок нет, создать номенклатуру?",РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ПослеПоказаВопросаОСоздании(РезультатВопроса, ДополнительныеПараметры) Экспорт 
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьНоменклатуруНаСервере(ДополнительныеПараметры);
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура СоздатьНоменклатуруНаСервере(МассивСтруктурНоменклатуры)
	
	Для Каждого СтрНоменклатуры Из МассивСтруктурНоменклатуры Цикл
		НоваяНоменклатура = Справочники.Номенклатура.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НоваяНоменклатура, СтрНоменклатуры);
		НоваяНоменклатура.СтавкаНДС = Справочники.СтавкиНДС.НайтиПоНаименованию("20%");
		НоваяНоменклатура.Записать();
		
		// добавить в регистр НДС от 01.01.2019
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Функция НачатьСопостовлениеНаСервере()
	
	// Очистим красные ячейки
	ТаблицаДляЗаполнения.Область(2,1,ТаблицаДляЗаполнения.ВысотаТаблицы,5).ЦветФона = Новый Цвет(255,255,255);
	
	ТаблицаЗагрузки = Новый ТаблицаЗначений;
	ОписаниеСтроки 	= Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(150));
	ОписаниеЧисла 	= Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(5,0));
	
	ТаблицаЗагрузки.Колонки.Добавить("НомерСтроки", ОписаниеЧисла);
	ТаблицаЗагрузки.Колонки.Добавить("Артикул", ОписаниеСтроки);
	ТаблицаЗагрузки.Колонки.Добавить("Наименование", ОписаниеСтроки);
	ТаблицаЗагрузки.Колонки.Добавить("ЕдиницаИзмерения", ОписаниеСтроки);
	ТаблицаЗагрузки.Колонки.Добавить("ЦеноваяГруппа", ОписаниеСтроки);
	ТаблицаЗагрузки.Колонки.Добавить("Производитель", ОписаниеСтроки);
		
	Для НомерСтроки=2 По ТаблицаДляЗаполнения.ВысотаТаблицы Цикл
		Если ПустаяСтрока(ТаблицаДляЗаполнения.Область(НомерСтроки,2).Текст) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТабЗнач = ТаблицаЗагрузки.Добавить();
		СтрокаТабЗнач.НомерСтроки		= НомерСтроки;
		СтрокаТабЗнач.Артикул 			= ТаблицаДляЗаполнения.Область(НомерСтроки,1).Текст;
		СтрокаТабЗнач.Наименование 		= ТаблицаДляЗаполнения.Область(НомерСтроки,2).Текст;
		СтрокаТабЗнач.ЕдиницаИзмерения 	= ТаблицаДляЗаполнения.Область(НомерСтроки,3).Текст;
		СтрокаТабЗнач.ЦеноваяГруппа 	= ТаблицаДляЗаполнения.Область(НомерСтроки,4).Текст;
		СтрокаТабЗнач.Производитель 	= ТаблицаДляЗаполнения.Область(НомерСтроки,5).Текст;
	КонецЦикла;
	
	//Поиск существующей номенклатуры по Наименованию
	Запрос = Новый Запрос;
	//Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаЗагрузки.НомерСтроки КАК НомерСтроки,
		|	ТаблицаЗагрузки.Артикул КАК Артикул,
		|	ТаблицаЗагрузки.Наименование КАК Наименование,
		|	ТаблицаЗагрузки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаЗагрузки.ЦеноваяГруппа КАК ЦеноваяГруппа,
		|	ТаблицаЗагрузки.Производитель КАК Производитель
		|ПОМЕСТИТЬ ВТ_ТаблицаЗагрузки
		|ИЗ
		|	&ТаблицаЗагрузки КАК ТаблицаЗагрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТаблицаЗагрузки.НомерСтроки КАК НомерСтроки,
		|	ВТ_ТаблицаЗагрузки.Артикул КАК Артикул,
		|	ВТ_ТаблицаЗагрузки.Наименование КАК Наименование,
		|	ВТ_ТаблицаЗагрузки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВТ_ТаблицаЗагрузки.ЦеноваяГруппа КАК ЦеноваяГруппа,
		|	ВТ_ТаблицаЗагрузки.Производитель КАК Производитель,
		|	ЕСТЬNULL(НоменклатураАрт.Ссылка, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК СсылкаПоАртикулу,
		|	ЕСТЬNULL(НоменклатураНаимен.Ссылка, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК СсылкаПоНаименованию,
		|	ЕСТЬNULL(Производители.Ссылка, ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК СсылкаПроизводитель,
		|	ЕСТЬNULL(УпаковкиЕдиницыИзмерения.Ссылка, НЕОПРЕДЕЛЕНО) КАК СсылкаЕдиницаИзмерения,
		|	ЕСТЬNULL(ЦеновыеГруппы.Ссылка, ЗНАЧЕНИЕ(Справочник.ЦеновыеГруппы.ПустаяСсылка)) КАК СсылкаЦеноваяГруппа
		|ИЗ
		|	ВТ_ТаблицаЗагрузки КАК ВТ_ТаблицаЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураНаимен
		|		ПО ВТ_ТаблицаЗагрузки.Наименование = НоменклатураНаимен.Наименование
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураАрт
		|		ПО ВТ_ТаблицаЗагрузки.Артикул = НоменклатураАрт.Артикул
		|			И (ВТ_ТаблицаЗагрузки.Артикул <> """")
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
		|		ПО ВТ_ТаблицаЗагрузки.ЕдиницаИзмерения = УпаковкиЕдиницыИзмерения.Наименование
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЦеновыеГруппы КАК ЦеновыеГруппы
		|		ПО ВТ_ТаблицаЗагрузки.ЦеноваяГруппа = ЦеновыеГруппы.Наименование
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Производители КАК Производители
		|		ПО ВТ_ТаблицаЗагрузки.Производитель = Производители.Наименование";
	
	Запрос.УстановитьПараметр("ТаблицаЗагрузки", ТаблицаЗагрузки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	МассивДанных = Новый Массив;
	ВернутьНеопределено = Ложь;
	Пока Выборка.Следующий() Цикл
		ОшибкаВСтроке = Ложь;
		//Найдем и расскрасим ошибки
		Если НЕ Выборка.СсылкаПоАртикулу.Пустая() Тогда
			ТаблицаДляЗаполнения.Область(Выборка.НомерСтроки,1).ЦветФона = Новый Цвет(255,0,0);
			ОшибкаВСтроке = Истина;
		КонецЕсли;
	
		Если НЕ Выборка.СсылкаПоНаименованию.Пустая() Тогда
			ТаблицаДляЗаполнения.Область(Выборка.НомерСтроки,2).ЦветФона = Новый Цвет(255,0,0);
			ОшибкаВСтроке = Истина;
		КонецЕсли;
		
		Если Выборка.СсылкаЕдиницаИзмерения = Неопределено Тогда
			ТаблицаДляЗаполнения.Область(Выборка.НомерСтроки,3).ЦветФона = Новый Цвет(255,0,0);
			ОшибкаВСтроке = Истина;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(Выборка.ЦеноваяГруппа) И Выборка.СсылкаЦеноваяГруппа.Пустая() Тогда
			ТаблицаДляЗаполнения.Область(Выборка.НомерСтроки,4).ЦветФона = Новый Цвет(255,0,0);
			ОшибкаВСтроке = Истина;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(Выборка.Производитель) И Выборка.СсылкаПроизводитель.Пустая() Тогда
			ТаблицаДляЗаполнения.Область(Выборка.НомерСтроки,5).ЦветФона = Новый Цвет(255,0,0);
			ОшибкаВСтроке = Истина;
		КонецЕсли;
		
		// Заполним таблицу для создания номенклатуры
		Если ОшибкаВСтроке Тогда
			ВернутьНеопределено = Истина;
			Продолжить;
		КонецЕсли;
		
		СтруктураДанныхНоменклатуры = Новый Структура;
		СтруктураДанныхНоменклатуры.Вставить("Наименование", 				Выборка.Наименование);
		СтруктураДанныхНоменклатуры.Вставить("НаименованиеПолное",          Выборка.Наименование);
		СтруктураДанныхНоменклатуры.Вставить("Производитель",               Выборка.СсылкаПроизводитель);
		СтруктураДанныхНоменклатуры.Вставить("ЦеноваяГруппа",               Выборка.СсылкаЦеноваяГруппа);
		СтруктураДанныхНоменклатуры.Вставить("Артикул",                     Выборка.Артикул);
		СтруктураДанныхНоменклатуры.Вставить("ЕдиницаИзмерения",            Выборка.СсылкаЕдиницаИзмерения);
		СтруктураДанныхНоменклатуры.Вставить("ВариантОформленияПродажи", 	Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг);
		СтруктураДанныхНоменклатуры.Вставить("Родитель", 					Справочники.Номенклатура.НайтиПоНаименованию("Товары"));
		СтруктураДанныхНоменклатуры.Вставить("ВидНоменклатуры",             ВидНоменклатуры);
		СтруктураДанныхНоменклатуры.Вставить("ИспользованиеХарактеристик",  Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать);
		СтруктураДанныхНоменклатуры.Вставить("Качество",                    Перечисления.ГрадацииКачества.Новый);
		СтруктураДанныхНоменклатуры.Вставить("ТипНоменклатуры",             Перечисления.ТипыНоменклатуры.Товар);
		СтруктураДанныхНоменклатуры.Вставить("ЕдиницаДляОтчетов",           Выборка.СсылкаЕдиницаИзмерения); 
		СтруктураДанныхНоменклатуры.Вставить("КоэффициентЕдиницыДляОтчетов",1);
		СтруктураДанныхНоменклатуры.Вставить("ОсобенностьУчета",			Перечисления.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета);
		СтруктураДанныхНоменклатуры.Вставить("ВестиУчетПоГТД",				Истина); 
		МассивДанных.Добавить(СтруктураДанныхНоменклатуры);
	КонецЦикла;
	
	Если ВернутьНеопределено Тогда
		Возврат Неопределено;
	Иначе
		Возврат МассивДанных;
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТаблицаДляЗаполнения.Область(1,1).Текст = "Артикул";
	ТаблицаДляЗаполнения.Область(1,1).ЦветФона = ЦветаСтиля.ЦветФонаШапкиОтчета;
	ТаблицаДляЗаполнения.Область(1,1).ШиринаКолонки = 20;
	ТаблицаДляЗаполнения.Область(1,2).Текст = "Наименование";
	ТаблицаДляЗаполнения.Область(1,2).ЦветФона = ЦветаСтиля.ДобавленныйРеквизитФон;
	ТаблицаДляЗаполнения.Область(1,2).ШиринаКолонки = 40;
	ТаблицаДляЗаполнения.Область(1,3).Текст = "Ед. измерения";
	ТаблицаДляЗаполнения.Область(1,3).ЦветФона = ЦветаСтиля.ДобавленныйРеквизитФон;
	ТаблицаДляЗаполнения.Область(1,4).Текст = "Ценовая группа";
	ТаблицаДляЗаполнения.Область(1,4).ЦветФона = ЦветаСтиля.ЦветФонаШапкиОтчета;
	ТаблицаДляЗаполнения.Область(1,5).Текст = "Производитель"; 
	ТаблицаДляЗаполнения.Область(1,5).ЦветФона = ЦветаСтиля.ЦветФонаШапкиОтчета;
	ТаблицаДляЗаполнения.Область(1,5).ШиринаКолонки = 20;

КонецПроцедуры
