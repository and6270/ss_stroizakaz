
&НаКлиенте
Процедура Расш6270_НайтиНаСайтеПосле(Команда)
	КодНоменклатуры = ПолучитьСвойствоНасервере(Элементы.СписокСтандартныйПоискНоменклатура.ТекущаяСтрока, "Код");
	ПерейтиПоНавигационнойСсылке("https://stroizakaz.su/magazin/search?sort_by=&search_text=" + КодНоменклатуры);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСвойствоНасервере(Ссылка, Свойство)
	Возврат Ссылка[Свойство];
КонецФункции

&НаКлиенте
Процедура Расш6270_СоздатьНаСайтеПосле(Команда)
	// Вставить содержимое обработчика.                       
	МассивНоменклатуры = Элементы.СписокСтандартныйПоискНоменклатура.ВыделенныеСтроки;
	Если МассивНоменклатуры.Количество() Тогда
		ТекстCSV = Расш6270_МегаГрупп.СоздатьТекстCSVПоНоменклатуре(МассивНоменклатуры); 
	Иначе 
		Возврат;
	КонецЕсли;   
	
	//Задать вопрос, показать структуру, и выгрузить на сайт 
	ТекстВопроса = "Создать номенклатуру на сайте?" 
					+ Символы.ПС 
					+ "Будет создано карточек номенклатуры: " 
					+ Строка(СтрЧислоСтрок(ТекстCSV) - 1)
					+ Символы.ПС 
					+ "ВНИМАНИЕ при повторной выгрузке или совподении кода 1С данные на сайте будут перезаписаны!";
	ОписаниеОповещенияВыгрузка = новый ОписаниеОповещения("ПослеВыбораОтветаДаНет", ЭтотОбъект, ТекстCSV);
	ПоказатьВопрос(ОписаниеОповещенияВыгрузка,ТекстВопроса,РежимДиалогаВопрос.ДаНет,,,"Создание номенклатуры на сайте");
КонецПроцедуры

&НаКлиенте 
Процедура ПослеВыбораОтветаДаНет(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Результат = Расш6270_МегаГрупп.ВыполнитьОбменССайтомКлиент(ДополнительныеПараметры); 
		Если Результат Тогда
			ПоказатьОповещениеПользователя("Номенклатура на сайт успешно выгружена");
		Иначе
			ПоказатьОповещениеПользователя("Ошибка выгрузки, повторите попозже или обратитесь к администратору");
		КонецЕсли;
		
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура Расш6270_ЗагрузитьИзТабличногоДокументаПосле(Команда)
	ОткрытьФорму("Обработка.ЗагрузкаНоменклатуры.Форма.Форма",,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.Независимый);
КонецПроцедуры

&НаСервереБезКонтекста
Функция  Расш6270_СделатьОсновнойМодификациейПослеНаСервере(МассивНоменклатуры, ОсновнаяМодификация)
	НачатьТранзакцию();
	Для каждого ТекНоменклатура из МассивНоменклатуры Цикл
		Попытка                  
			ОбъектНоменклатура = ТекНоменклатура.ПолучитьОбъект();
			ОбъектНоменклатура.Заблокировать();
			ОбъектНоменклатура.Расш6270_ОсновнаяМодификация = ОсновнаяМодификация;
			ОбъектНоменклатура.Расш6270_ВыгружатьНаСайт = Истина;
			ОбъектНоменклатура.Записать();
		Исключение
			Сообщить("Не удалось выполнить запись в номенклатуру: " + ТекНоменклатура + ", текст ошибки: " + ОписаниеОшибки());
			ОтменитьТранзакцию(); 
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура Расш6270_СделатьОсновнойМодификациейПосле(Команда)
	ОсновнаяМодификация = Элементы.СписокСтандартныйПоискНоменклатура.ТекущаяСтрока;
	МассивНоменклатуры = Элементы.СписокСтандартныйПоискНоменклатура.ВыделенныеСтроки;
	
	Результат = Расш6270_СделатьОсновнойМодификациейПослеНаСервере(МассивНоменклатуры, ОсновнаяМодификация);
	Если Результат Тогда
		ПоказатьОповещениеПользователя("Основная модификация успешно изменена");
		Элементы.СписокСтандартныйПоискНоменклатура.Обновить();
	КонецЕсли;
	
КонецПроцедуры
